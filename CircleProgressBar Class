Option Explicit

'Circle Progress Barのメインラベル、ステータスラベル、色（RGB値）
Public MainLabel As String
Public StatusLabel As String
Public BarColor As Long

'Circle Progress Barの最大値
Public Value As Long
Public Max As Long

'Circle Progress Barのメーターが100%になったときに
'チェックマークを表示する場合True（規定値）、しない場合False を指定する
Public AddCheckMark As Boolean


'Circle Progress Barを表示するワークシート
Private cpbWorksheet As Worksheet

'Circle Progress Barの表示位置
Private Left As Single
Private Top As Single

'Circle Progress Barを構成する図形
Private backLine As Shape
Private meterBar As Shape
Private percentBox As Shape
Private mainLabelBox As Shape
Private statusLabelBox As Shape
Private checkMark As Shape

'生成する画像の共通代替テキスト（画像の消去に使う）
Private cpbAlternativeText As String

'Circle Progress Barのデザイン定数
Private Const cpbStartAngle As Double = 130
Private Const cpbEndAngle As Double = 50
Private Const cpbEffectiveAngle As Double = 360 - cpbStartAngle + cpbEndAngle
Private Const cpbBarWeight As Double = 0.1
    
    
'-------------------------------------------------------------------------------
'Circle Progress Barを開く
'-------------------------------------------------------------------------------
Public Sub OpenBar(Optional ByVal BarLeft As Variant, _
                   Optional ByVal BarTop As Variant, _
                   Optional ByVal BarWorksheet As Worksheet = Nothing)

    '画像の共通代替テキストを生成する
    cpbAlternativeText = "cpb_" & CStr(ObjPtr(Me))
    
    If Not IsMissing(BarLeft) Then Left = BarLeft
    If Not IsMissing(BarTop) Then Top = BarTop

    If BarWorksheet Is Nothing Then
        Set cpbWorksheet = ActiveSheet
    Else
        Set cpbWorksheet = BarWorksheet
    End If

    'メーターの背景線
    Set backLine = cpbWorksheet.Shapes.AddShape(msoShapeBlockArc, Left, Top, 150, 150)
    With backLine
        .AlternativeText = cpbAlternativeText
        .Adjustments.Item(1) = cpbStartAngle
        .Adjustments.Item(2) = cpbEndAngle
        .Adjustments.Item(3) = cpbBarWeight
        .Fill.ForeColor.RGB = rgbWhite
        .Fill.Transparency = 0.2
        .Line.Weight = 0.5
        .Line.ForeColor.RGB = rgbGray
    End With
    
    'メーターバー
    Set meterBar = cpbWorksheet.Shapes.AddShape(msoShapeBlockArc, Left, Top, 150, 150)
    With meterBar
        .AlternativeText = cpbAlternativeText
        .Line.Visible = msoFalse
        .Fill.ForeColor.RGB = rgbWhite
        .Adjustments.Item(1) = cpbStartAngle
        .Adjustments.Item(2) = -(360 - cpbStartAngle - 1)
        .Adjustments.Item(3) = cpbBarWeight
        With .Shadow
            .Type = msoShadow25
            .Visible = msoTrue
            .Style = msoShadowStyleOuterShadow
            .Blur = 15
            .OffsetX = 8
            .OffsetY = 8
            .RotateWithShape = msoTrue
            .ForeColor.RGB = rgbBlack
            .Transparency = 0.1
        End With
    End With

    'パーセント表示ボックス
    Set percentBox = cpbWorksheet.Shapes.AddTextbox(msoTextOrientationHorizontal, Left + 30, Top + 50, 95, 43)
    With percentBox
        .AlternativeText = cpbAlternativeText
        .Fill.Visible = msoFalse
        .Line.Visible = msoFalse
        With .TextFrame2
            .VerticalAnchor = msoAnchorMiddle
            .TextRange.Characters.Text = "0%"
            .TextRange.ParagraphFormat.Alignment = msoAlignCenter
            .TextRange.Font.Name = "Cooper Black"
            .TextRange.Font.Size = 30
            With .TextRange.Font.Glow
                .Color.RGB = rgbWhite
                .Color.TintAndShade = 0
                .Color.Brightness = 0
                .Transparency = 0
                .Radius = 30
            End With
        End With
    End With

    'タイトル
    Set mainLabelBox = cpbWorksheet.Shapes.AddTextbox(msoTextOrientationHorizontal, Left + 5, Top + 135, 140, 20)
    With mainLabelBox
        .AlternativeText = cpbAlternativeText
        .Fill.Visible = msoFalse
        .Line.Visible = msoFalse
        With .TextFrame2
            .VerticalAnchor = msoAnchorMiddle
            .TextRange.Characters.Text = MainLabel
            .TextRange.ParagraphFormat.Alignment = msoAlignCenter
            .TextRange.Font.Name = "Arial"
            .TextRange.Font.Size = 16
            .TextRange.Font.Bold = msoTrue
            With .TextRange.Font.Glow
                .Color.RGB = rgbWhite
                .Color.TintAndShade = 0
                .Color.Brightness = 0
                .Transparency = 0
                .Radius = 20
            End With
        End With
    End With

    'サブタイトル
    Set statusLabelBox = cpbWorksheet.Shapes.AddTextbox(msoTextOrientationHorizontal, Left + 5, Top + 157, 140, 20)
    With statusLabelBox
        .AlternativeText = cpbAlternativeText
        .Fill.Visible = msoFalse
        .Line.Visible = msoFalse
        With .TextFrame2
            .VerticalAnchor = msoAnchorMiddle
            .TextRange.Characters.Text = StatusLabel
            .TextRange.ParagraphFormat.Alignment = msoAlignCenter
            .TextRange.Font.Name = "Arial"
            .TextRange.Font.Size = 16
            .TextRange.Font.Bold = msoTrue
            With .TextRange.Font.Glow
                .Color.RGB = rgbWhite
                .Color.TintAndShade = 0
                .Color.Brightness = 0
                .Transparency = 0
                .Radius = 20
            End With
        End With
    End With
    
    DoEvents

End Sub


'-------------------------------------------------------------------------------
'Circle Progress Barのメーターを更新する
'-------------------------------------------------------------------------------
Public Sub UpdateBar(Optional ByVal BarValue As Variant, _
                     Optional ByVal BarStatus As Variant)
    
    If Not IsMissing(BarValue) Then Value = BarValue
    If Not IsMissing(BarStatus) Then StatusLabel = BarStatus
    With meterBar
        If (Value / Max) >= (1 / cpbEffectiveAngle) Then
            If Value >= Max Then
                Value = Max
                If AddCheckMark Then PutCheckMark
            End If
            .Adjustments.Item(2) = -(360 - cpbStartAngle) + cpbEffectiveAngle * Value / Max
            .Fill.ForeColor.RGB = BarColor
        Else
            .Adjustments.Item(2) = -(360 - cpbStartAngle - 1)
            .Fill.ForeColor.RGB = rgbWhite
        End If
    End With
    percentBox.TextFrame2.TextRange.Characters.Text = CStr(Int(Value / Max * 100)) & "%"
    statusLabelBox.TextFrame2.TextRange.Characters.Text = StatusLabel
    DoEvents
End Sub


'-------------------------------------------------------------------------------
'Circle Progress Barを閉じる（生成した画像を消去する）
'-------------------------------------------------------------------------------
Public Sub CloseBar()
    Dim s As Shape
    For Each s In cpbWorksheet.Shapes
        If s.AlternativeText = cpbAlternativeText Then s.Delete
    Next
End Sub


'-------------------------------------------------------------------------------
'Circle Progress Barにチェックマークを表示する
'-------------------------------------------------------------------------------
Private Sub PutCheckMark()
    If Not checkMark Is Nothing Then Exit Sub
    Set checkMark = cpbWorksheet.Shapes.AddShape(msoShapeCorner, Left + 66, Top + 95, 28, 16)
    With checkMark
        .AlternativeText = cpbAlternativeText
        .IncrementRotation -42
        .Adjustments.Item(1) = 0.25
        .Adjustments.Item(2) = 0.25
        .Line.Visible = msoFalse
        .Fill.Visible = msoTrue
        .Fill.ForeColor.RGB = rgbGray
    End With
    DoEvents
End Sub


'-------------------------------------------------------------------------------
'Circle Progress Barの初期値を設定する
'-------------------------------------------------------------------------------
Private Sub Class_Initialize()
    Left = 100
    Top = 100
    MainLabel = "Main Label"
    StatusLabel = "Status Label"
    BarColor = rgbRoyalBlue
    Value = 0
    Max = 100
    AddCheckMark = True
End Sub


'-------------------------------------------------------------------------------
'Circle Progress Barの後処理（現時点では何もしない）
'-------------------------------------------------------------------------------
Private Sub Class_Terminate()

End Sub
